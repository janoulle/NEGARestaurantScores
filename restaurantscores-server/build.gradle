buildscript {
    ext {
        projectVersion = "$projectVersionId-$projectVersionType"
        libraries = [
                jcl_over_slf4j         : 'org.slf4j:jcl-over-slf4j:1.7.12',
                logback                : 'ch.qos.logback:logback-classic:1.2.3',
                janino                 : 'org.codehaus.janino:janino:3.0.6',
                lombok                 : 'org.projectlombok:lombok:1.18.0',
                postgresql             : 'org.postgresql:postgresql',
                tomcat                 : 'org.springframework.boot:spring-boot-starter-tomcat',
                spring_actuator        : 'org.springframework.boot:spring-boot-starter-actuator',
                spring_jpa             : 'org.springframework.boot:spring-boot-starter-data-jpa',
                spring_cache           : 'org.springframework.boot:spring-boot-starter-cache',
                spring_web             : 'org.springframework.boot:spring-boot-starter-web',
                spring_test            : 'org.springframework.boot:spring-boot-starter-test',
                spring_rest            : 'org.springframework.boot:spring-boot-starter-data-rest',
                guava                  : 'com.google.guava:guava:25.1-jre',
                junit                  : 'junit:junit:4.12',
                javax_inject           : 'javax.inject:javax.inject:1',
                joda_time              : 'joda-time:joda-time:2.9.4',
                commons_lang           : 'org.apache.commons:commons-lang3:3.4',
                jsoup                  : 'org.jsoup:jsoup:1.11.3',
                commons_io             : 'commons-io:commons-io:2.4',
                firebase_admin         : 'com.google.firebase:firebase-admin:4.1.6',
                amazon_aws_sdk_s3      : 'com.amazonaws:aws-java-sdk-s3',
                amazon_aws_sdk_ec2     : 'com.amazonaws:aws-java-sdk-ec2',
                amazon_aws_sdk_dynamodb: 'com.amazonaws:aws-java-sdk-dynamodb',
                elasticsearch_rest     : 'org.elasticsearch.client:elasticsearch-rest-client-sniffer:5.6.1',
                spring_devtools        : 'org.springframework.boot:spring-boot-devtools',
                h2                     : 'com.h2database:h2'
        ]
    }
}

// https://docs.gradle.org/current/userguide/plugins.html
plugins {
    id "jacoco"
    id "war"
    id "eclipse"
    id "org.springframework.boot" version "2.0.3.RELEASE"
    id "io.spring.dependency-management" version "1.0.5.RELEASE"
    id "org.sonarqube" version "2.6.2"
    id "com.palantir.docker" version "0.19.2"
}

apply from: 'palantirdockertasks.gradle'
apply from: 'releasemanagement.gradle'

//Transmode docker tasks
//apply plugin: 'docker'
//apply from: 'transmodedockertasks.gradle'

//https://stackoverflow.com/questions/17262856/gradle-how-to-set-the-project-name-group-version-plus-source-targetcompatibi
group = projectGroup
version = projectVersion

configurations {
    providedRuntime
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        //https://github.com/awsdocs/aws-doc-sdk-examples
        mavenBom 'com.amazonaws:aws-java-sdk-bom:1.11.228'
    }
}


dependencies {
    compile libraries.jcl_over_slf4j
    compile libraries.logback
    compile libraries.janino

    compileOnly libraries.lombok

    compile libraries.javax_inject
    compile libraries.spring_jpa
    compile libraries.spring_cache
    compile libraries.spring_actuator
    compile libraries.spring_web
    compile libraries.spring_rest
    compile libraries.guava
    compile libraries.joda_time
    compile libraries.commons_lang
    compile libraries.jsoup
    compile libraries.commons_io

    //https://firebase.google.com/support/release-notes/admin/java
    compile libraries.firebase_admin
    compile libraries.amazon_aws_sdk_s3
    compile libraries.amazon_aws_sdk_ec2
    compile libraries.amazon_aws_sdk_dynamodb
    compile libraries.elasticsearch_rest

    compile libraries.spring_devtools

    testCompile libraries.spring_test

    //http://www.baeldung.com/spring-boot-testing needed for @datajpatest annotation
    testCompile libraries.h2
    runtime libraries.postgresql
    providedRuntime libraries.tomcat


}

jacocoTestReport {
    dependsOn test
    reports {
        xml{
            enabled true
            destination "${buildDir}/reports/jacoco/xml/jacoco.xml"
        }
        csv.enabled false
        html{
            enabled true
            destination "${buildDir}/reports/jacoco/html"
        }
    }
}