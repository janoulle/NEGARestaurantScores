

/*http://mrhaki.blogspot.com/2009/10/groovy-goodness-executing-string-or.html
  http://mrhaki.blogspot.com/2010/10/gradle-goodness-parse-output-from-exec.html */
static String executeCmd(String cmd) {
    def command = cmd?.execute()

    return command?.text?.trim()
}

//https://stackoverflow.com/questions/11657295/count-the-number-of-commits-on-a-git-branch
//https://stackoverflow.com/questions/15798862/what-does-git-rev-parse-do
def getBuildInfo() {

    String lastCommitId = executeCmd("git rev-parse HEAD")
    String tag = executeCmd("git name-rev --tags --name-only $lastCommitId")

    def buildInfo = [
            'Main-Class' : 'com.janeullah.healthinspectionrecords.RestaurantScoresApplication',
            'Implementation-Title'       : projectName,
            'Implementation-Version'     : projectVersion,
            'Build-Target'               : projectVersionType,
            'Build-Time'                 : new Date().format("yyyy-MM-dd'T'HH:mm'Z'"),
            'Git-Branch'                 : executeCmd("git rev-parse --abbrev-ref HEAD"),
            'Git-Branch-Commit-Count'    : executeCmd("git rev-list --count HEAD"),
            'Git-Last-Commit-Id'         : lastCommitId,
            'Git-Branch-Tag'             : tag == 'undefined' ? 'None' : tag,
            'Gradle-Version'             : GradleVersion.current().getVersion(),
            'Java-Version'               : org.gradle.internal.jvm.Jvm.current()
    ]

    buildInfo
}

/*https://stackoverflow.com/questions/8012048/groovy-write-out-the-list-to-a-file
https://docs.gradle.org/current/userguide/tutorial_using_tasks.html#sec:using_methods */
task writeBuildInfo () {
    doLast {
        def buildInfo = getBuildInfo()

        String outputPath = "${projectDir}/build.info"
        File file = new File(outputPath)
        file.withWriter{ out ->
            buildInfo.each {k, v -> out.println "${k}:${v}"}
        }
    }
}

//https://docs.gradle.org/current/userguide/tutorial_using_tasks.html#sec:build_scripts_are_code
/*task distribution {
    doLast {
        println "We build the zip with version=$version"
    }
}

task release(dependsOn: 'distribution') {
    doLast {
        println 'We release now'
    }
}

gradle.taskGraph.whenReady {taskGraph ->
    if (taskGraph.hasTask(release)) {
        ext.version = '1.0'
    } else {
        version = '1.0-SNAPSHOT'
    }
}*/

private String getRemoteDetails(String dockerImageName, String dockerImageVersion, String key, String value) {
    def remoteDetails = ''
    if (key == "docker") {
        remoteDetails = "$value:$dockerImageVersion"
    } else if (key == "microsoft" || key == "amazon") {
        remoteDetails = "$value/$dockerImageName:$dockerImageVersion"
    } else if (key == "heroku") {
        remoteDetails = "$value"
    }
    println remoteDetails
    remoteDetails
}


def dockerTag(String dockerImageName, String dockerImageVersion, String key, String value) {
    def remoteDetails = getRemoteDetails(dockerImageName, dockerImageVersion, key, value)
    exec {
        executable "docker"
        args "tag", "$dockerImageName:$dockerImageVersion", "$remoteDetails"
    }
}

//https://stackoverflow.com/questions/22449649/run-shell-command-in-gradle-but-not-inside-a-task
def dockerPush(String dockerImageName, String dockerImageVersion, String key, String value) {

    def remoteDetails = getRemoteDetails(dockerImageName, dockerImageVersion, key, value)
    exec {
        executable "docker"
        args "push", "$remoteDetails"
    }
}

task buildAndTagImage(dependsOn: ['clean','docker']) {
    doLast {
        //file("../Docker/restaurantscores/repos.json")

        def remoteRegistries = file("$pathToRepos")
        def parsedJson = new groovy.json.JsonSlurper().parseText(remoteRegistries?.text)

        parsedJson?.each { key, value ->
            println "value:$value"
            dockerTag(dockerImageName, dockerImageVersion, key, value)
            dockerPush(dockerImageName, dockerImageVersion, key, value)
        }
    }
}